buildscript { repositories { jcenter() } }
plugins {
    id 'nebula.lint' version '6.7.0'
}

group 'com.upserve'
version '1.0-SNAPSHOT'

description = """Uppend: fast, append-only key-value store"""

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

gradleLint.rules += 'unused-dependency'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'it.unimi.dsi:fastutil:7.0.13'
    compile 'org.slf4j:slf4j-api:1.7.22'

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.logging.log4j:log4j-api:2.8'
    testCompile 'org.mockito:mockito-core:2.7.13'

    gradleLint.ignore {
      // gradle-lint doesn't like lombok, presumably because it's annotation-only
      compile 'org.projectlombok:lombok:1.16.12'

      // gradle-lint doesn't like log4j, presumably because it's not used directly
      testCompile 'org.apache.logging.log4j:log4j-core:2.8'
      testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.8'
    }
}

sourceSets {
    main {
        output.resourcesDir = "${buildDir}/classes/main"
    }
    test {
        output.resourcesDir = "${buildDir}/classes/test"
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.upserve.uppend.Uppend'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
